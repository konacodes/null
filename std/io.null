-- std/io.null
-- Standard I/O operations for null

@extern "C" do
    fn puts(s :: ptr<u8>) -> i32
    fn printf(fmt :: ptr<u8>) -> i32
    fn putchar(c :: i64) -> i64
    fn getchar() -> i64
end

-- Print a string followed by newline
fn print(s :: ptr<u8>) -> void do
    puts(s)
end

-- Print a string without newline
fn print_raw(s :: ptr<u8>) -> void do
    printf(s)
end

-- Print an integer (handles negative numbers)
fn print_int(n :: i64) -> void do
    if n < 0 do
        putchar(45)
        n = 0 - n
    end

    if n == 0 do
        putchar(48)
        ret
    end

    mut temp :: i64 = n
    mut divisor :: i64 = 1
    while temp >= 10 do
        temp = temp / 10
        divisor = divisor * 10
    end

    while divisor > 0 do
        let digit :: i64 = n / divisor
        putchar(48 + digit)
        n = n - digit * divisor
        divisor = divisor / 10
    end
end

-- Print a newline
fn println() -> void do
    putchar(10)
end

-- Read a character from stdin
fn read_char() -> i64 do
    ret getchar()
end
